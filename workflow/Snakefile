
from collections import defaultdict

def read_groups(glob):
    reads ,= glob_wildcards(glob)
    groups = {}
    for read in reads:
        group_name, lane = read.rsplit('_', 1)
        groups[read] = group_name
    return groups

paired = read_groups("rnaseq/paired/{reads}_1.fq.gz")
single = read_groups("rnaseq/single/{reads}_R1_001.fastq.gz")
contam ,= glob_wildcards("contamination/{query}.fna")
vir_clades ,= glob_wildcards("viral_elements/{clade}.gff3")
species ,= glob_wildcards("databases/uniprot/species/{species}.fasta")

rule all:
    input:
        "analysis/braker/braker.gtf"

rule get_homologs:
    input:
        "references"
    output:
        directory("analysis/references/homologs")
    params:
        min_taxa = 3
    shadow:
        "minimal"
    conda:
        "envs/get_homologs.yaml"
    threads:
        40
    shell:
        "get_homologues.pl -M -t {params.min_taxa} -d {input} -n {threads} -X && mv {input}_homologues {output}"

rule star_index:
    input:
        "analysis/assembly/genome.fa"
    output:
        directory("analysis/star/genome")
    conda:
        "envs/star.yaml"
    shell:
        "STAR --genomeSAindexNbases 10 --runMode genomeGenerate --genomeFastaFiles {input} --genomeDir {output}"

rule star_map_single:
    input:
        index = "analysis/star/genome",
        reads = expand("analysis/rnaseq/trimmed/single/{reads}_R1_001_trimmed.fq.gz", reads = single.keys())
    output:
        "analysis/star/single.Aligned.sortedByCoord.out.bam"
    params:
        prefix = "analysis/star/single",
        reads = lambda w, input: ','.join(input.reads),
        groups = lambda w: ' , '.join([ "ID:" + x for x in single.values() ])
    conda:
        "envs/star.yaml"
    threads:
        20
    shell:
        "STAR --runThreadN {threads} --genomeDir {input.index} --readFilesCommand zcat --readFilesIn {params.reads} --outSAMattrRGline {params.groups} --outFileNamePrefix {params.prefix}. --outSAMtype BAM SortedByCoordinate"

rule star_map_paired:
    input:
        index = "analysis/star/genome",
        left  = expand("analysis/rnaseq/trimmed/paired/{reads}_{side}_val_{side}.fq.gz", reads = paired.keys(), side = 1),
        right = expand("analysis/rnaseq/trimmed/paired/{reads}_{side}_val_{side}.fq.gz", reads = paired.keys(), side = 2)
    output:
        "analysis/star/paired.Aligned.sortedByCoord.out.bam"
    params:
        prefix = "analysis/star/paired",
        left = lambda w, input: ','.join(input.left),
        right = lambda w, input: ','.join(input.right),
        groups = lambda w: ' , '.join([ "ID:" + x for x in paired.values() ])
    conda:
        "envs/star.yaml"
    threads:
        20
    shell:
        "STAR --runThreadN {threads} --genomeDir {input.index} --readFilesCommand zcat --readFilesIn {params.left} {params.right} --outSAMattrRGline {params.groups} --outFileNamePrefix {params.prefix}. --outSAMtype BAM SortedByCoordinate"

rule trim_paired:
    input:
        "rnaseq/paired/{reads}_1.fq.gz",
        "rnaseq/paired/{reads}_2.fq.gz"
    output:
        "analysis/rnaseq/trimmed/paired/{reads}_1_val_1.fq.gz",
        "analysis/rnaseq/trimmed/paired/{reads}_2_val_2.fq.gz"
    params:
        dirname = "analysis/rnaseq/trimmed/paired"
    conda:
        "envs/trim_galore.yaml"
    shell:
        "trim_galore --paired {input} -o {params.dirname}"

rule trim_single:
    input:
        "rnaseq/single/{reads}_R1_001.fastq.gz"
    output:
        "analysis/rnaseq/trimmed/single/{reads}_R1_001_trimmed.fq.gz"
    params:
        dirname = "analysis/rnaseq/trimmed/single"
    shell:
        "trim_galore {input} -o {params.dirname}"

rule cat_homologs:
    input:
        "analysis/references/homologs"
    output:
        "analysis/references/homologs.faa"
    conda:
        "envs/tools.yaml"
    shell:
        "find {input} -name '*.faa' | xargs cat > {output}"

rule cdhit_homologs:
    input:
        "analysis/references/homologs.faa"
    output:
        "analysis/references/homologs.cdhit"
    params:
        c = 0.9
    conda:
        "envs/tools.yaml"
    shell:
        "cdhit -i {input} -o {output} -c {params.c} -d 0"

rule bastx_homologs:
    input:
        homologs = "analysis/references/homologs.cdhit",
        assembly = "analysis/assembly/genome.fa"
    output:
        "analysis/references/homologs.blast6"
    log:
        "analysis/references/homologs.log"
    params:
        evalue = 1e-10
    conda:
        "envs/diamond.yaml"
    threads:
        20
    shell:
        "diamond blastx -k 100000 --max-hsps 100000 --query {input.assembly} --db {input.homologs} --outfmt 6 --out {output} --threads {threads} --evalue {params.evalue} &> {log}"

rule link_genome:
    input:
        "assembly"
    output:
        "analysis/assembly/genome.fa"
    shell:
        "ln -rs {input}/* {output}"

rule red:
    input:
        "analysis/assembly/genome.fa"
    output:
        sco = "analysis/red/sco/genome.scr",
        cnd = "analysis/red/cnd/genome.cnd",
        rpt = "analysis/red/rpt/genome.bed",
        msk = "analysis/red/msk/genome.msk",
        tbl = "analysis/red/red.tbl",
        hmm = "analysis/red/red.hmm"
    params:
        genome = "analysis/assembly",
        scodir = "analysis/red/sco",
        cnddir = "analysis/red/cnd",
        rptdir = "analysis/red/rpt",
        mskdir = "analysis/red/msk"
    conda:
        "envs/red.yaml"
    shell:
        """
        mkdir -p {params.scodir} {params.cnddir} {params.rptdir} {params.mskdir}
        Red -gnm {params.genome} -tbl {output.tbl} -hmo {output.hmm} -sco {params.scodir} -cnd {params.cnddir} -rpt {params.rptdir} -msk {params.mskdir} -frm 2
        """

rule makeblastdb:
    input:
        "analysis/assembly/genome.fa"
    output:
        "analysis/assembly/genome.fa.ndb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype nucl"

rule blast_contam:
    input:
        db = "analysis/assembly/genome.fa",
        ndb = "analysis/assembly/genome.fa.ndb",
        query = "contamination/{query}.fna"
    output:
        "analysis/contamination/{query}.blast6"
    params:
        evalue = 1e-20
    conda:
        "envs/blast.yaml"
    shell:
        "blastn -query {input.query} -db {input.db} -evalue {params.evalue} -outfmt 6 -out {output}"

rule bed_contam:
    input:
        "analysis/contamination/{query}.blast6"
    output:
        "analysis/contamination/{query}.bed"
    params:
        min_len = 1000
    conda:
        "envs/tools.yaml"
    shell:
        "awk '{{s=$9;e=$10}} $9>$10 {{s=$10;e=$9}} $4>{params.min_len} {{print$2,s-1,e}}' OFS=\\\\t {input} > {output}"

rule bed_homologs:
    input:
        "analysis/references/homologs.blast6"
    output:
        "analysis/references/homologs.bed"
    conda:
        "envs/tools.yaml"
    shell:
        "awk '{{s=$7;e=$8}} $7>$8 {{s=$8;e=$7}} {{print$1,s,e}}' OFS=\\\\t {input} | bedtools sort | bedtools merge > {output}"

rule mask_bed:
    input:
        assembly = "analysis/assembly/genome.fa",
        red_bed  = "analysis/red/rpt/genome.bed",
        homologs = "analysis/references/homologs.bed",
        viruses  = expand("viral_elements/{clade}.gff3", clade = vir_clades),
        contam   = expand("analysis/contamination/{query}.bed", query = contam)
    output:
        "analysis/mask/masked.bed"
    conda:
        "envs/tools.yaml"
    shell:
        """
        bedtools merge \
                -i <(bedtools subtract -a {input.red_bed} -b {input.homologs}) \
                -i <(cat {input.contam}  | bedtools sort) \
                -i <(cat {input.viruses} | bedtools sort) \
        > {output}
        """

rule mask_fasta:
    input:
        fasta = "analysis/assembly/genome.fa",
        bed = "analysis/mask/masked.bed"
    output:
        "analysis/mask/masked.fasta"
    conda:
        "envs/tools.yaml"
    shell:
        "bedtools maskfasta -soft -fi {input.fasta} -bed {input.bed} -fo {output}"

rule braker:
    input:
        masked = "analysis/mask/masked.fasta",
        paired = "analysis/star/paired.Aligned.sortedByCoord.out.bam",
        single = "analysis/star/single.Aligned.sortedByCoord.out.bam",
        homologs = "analysis/references/homologs.cdhit"
    output:
        "analysis/braker/braker.gtf"
    params:
        species = "genome_red_subtract",
        dirname = "analysis/braker",
        gc_prob = 0.05
    singularity:
        "docker://teambraker/braker3"
    threads:
        20
    shell:
        "braker --genome {input.masked} --species {params.species} --bam {input.paired},{input.single} --prot_seq {input.homologs} --workingdir {params.dirname} --etpmode --cores {threads} --addUTR on --softmasking --gc_probability {params.gc_prob}"

rule trna:
    input:
        "analysis/assembly/genome.fa"
    output:
        tab = "analysis/ncrna/trna.tab",
        sec = "analysis/ncrna/trna.sec"
    log:
        "analysis/ncrna/trna.log"
    conda:
        "envs/trnascan.yaml"
    shell:
        "tRNAscan-SE -o {output.tab} -f {output.sec} {input} &> {log}"

rule rrna:
    input:
        fasta = "analysis/assembly/genome.fa",
        cm = "databases/Rfam/Rfam.cm",
        clanin = "databases/Rfam/Rfam.clanin"
    output:
        txt = "analysis/ncrna/rrna.txt",
        tblout = "analysis/ncrna/rrna.tblout"
    log:
        "analysis/ncrna/rrna.log"
    conda:
        "envs/infernal.yaml"
    threads:
        4
    shell:
        "cmscan --cpu {threads} --cut_ga --rfam --nohmmonly --clanin {input.clanin} --oskip --fmt 2 -o {output.txt} --tblout {output.tblout} databases/Rfam/Rfam.cm {input.fasta} &> {log}"

# stub
rule busco:
    input:
        "analysis/braker/braker.faa"
    output:
        "analysis/species/{taxon}/busco/{name}/run_{lineage}/full_table.tsv",
        "analysis/species/{taxon}/busco/{name}/short_summary.specific.{lineage}.busco.txt",
        directory("analysis/species/{taxon}/busco/{name}/logs_{lineage}"),
        directory("analysis/species/{taxon}/busco/{name}/run_{lineage}/busco_sequences/single_copy_busco_sequences/")
    params:
        dir = "analysis/species/{taxon}/busco/{name}"
    shadow:
        "minimal"
    conda:
        "envs/busco.yaml"
    threads:
        10
    shell:
        """
        busco --lineage_dataset {wildcards.lineage} -f -i {input} -o busco -m protein -c {threads}
        mv busco/logs busco/logs_{wildcards.lineage}
        rsync -qav busco/ {params.dir}
        """

rule proteinortho:
    input:
        expand("analysis/species/{species}.fasta", species = species)
    output:
        "analysis/proteinortho/proteinortho.proteinortho.tsv"
    shadow:
        "minimal"
    params:
        dir = "analysis/proteinortho",
        evalue = 1e-5
    threads:
        30
    conda:
        "envs/proteinortho.yaml"
    shell:
        """
        proteinortho -project=proteinortho -cpus={threads} -e={params.evalue} {input}
        mv proteinortho.* {params.dir}
        """

rule rhod_interproscan:
    input:
        "analysis/braker/braker.faa"
    output:
        "analysis/interproscan/braker.faa.tsv"
    params:
        dir = "analysis/interproscan"
    threads:
        20
    shell:
        "interproscan.sh -i {input} -d {params.dir} -cpu {threads}"
